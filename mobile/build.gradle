plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'de.michelinside.glucodatahandler'
    compileSdk rootProject.compileSdk

    defaultConfig {
        applicationId "de.michelinside.glucodatahandler"
        minSdk rootProject.minSdk
        targetSdk rootProject.targetSdk
        versionCode 1000 + rootProject.versionCode
        versionName rootProject.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            // Enable ProGuard
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "GlucoDataHandler"
        }
        dev_release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-dev-rules.pro'
            versionNameSuffix '-dev.' + rootProject.versionCode
            resValue "string", "app_name", "GlucoDataHandler"
            signingConfig signingConfigs.debug
        }
        debug {
            minifyEnabled false
            resValue "string", "app_name", "GlucoDataHandler"
        }
        second {
            applicationIdSuffix '.second'
            versionNameSuffix '_SECOND'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "GDH Second"
            signingConfig signingConfigs.debug
        }
        applicationVariants.all {
                // this method is use to rename your all apk weather
                // it may be signed or unsigned(debug apk)
            variant ->
                variant.outputs.each {
                        // on below line we are setting a
                        // name to our apk as GlucoDataHandler.apk
                    output ->
                        def name = "GlucoDataHandler_" + versionName + ".apk"
                        // on below line we are setting the
                        // outputFile Name to our apk file.
                        output.outputFileName = name
                }
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'com.joaomgcd:taskerpluginlibrary:0.4.10'
    implementation project(path: ':common')
    implementation 'com.google.android.gms:play-services-tasks:18.2.0'
    implementation 'com.google.android.gms:play-services-wearable:19.0.0'
    implementation "androidx.car.app:app:1.4.0"
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation "com.jaredrummler:colorpicker:1.1.0"
    implementation "androidx.media:media:1.7.0"
    implementation 'androidx.work:work-runtime:2.9.1'
    implementation "com.ncorti:slidetoact:0.11.0"
    implementation 'com.takisoft.preferencex:preferencex-datetimepicker:1.1.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

afterEvaluate {
    //noinspection ConfigurationAvoidance
    def bundleRelease = tasks.getByPath(':mobile:bundleRelease')
    def copyAndroidBundlePostBuild = tasks.register('copyAndroidBundlePostBuild', Copy) {
        dependsOn bundleRelease
        from "${projectDir}/release"
        include '**/*.aab'
        into rootProject.releasePath + "/Playstore" + "/" + versionCode.toString()
    }
    bundleRelease.finalizedBy(copyAndroidBundlePostBuild)

    //noinspection ConfigurationAvoidance
    def bundleDev_release = tasks.getByPath(':mobile:bundleDev_release')
    def copyAndroidBundleDevPostBuild = tasks.register('copyAndroidBundleDevPostBuild', Copy) {
        dependsOn bundleDev_release
        from "${projectDir}/dev_release"
        include '**/*.aab'
        into rootProject.releasePath + "/Playstore" + "/" + versionCode.toString()
    }
    bundleDev_release.finalizedBy(copyAndroidBundleDevPostBuild)

    //noinspection ConfigurationAvoidance
    def assembleRelease = tasks.getByPath(':mobile:assembleRelease')
    def copyAndroidApksPostBuild = tasks.register('copyAndroidApksPostBuild', Copy) {
        dependsOn assembleRelease
        from "${projectDir}/release"
        include '**/*.apk'
        into rootProject.releasePath
    }
    assembleRelease.finalizedBy(copyAndroidApksPostBuild)

    //noinspection ConfigurationAvoidance
    def assembleDevRelease = tasks.getByPath(':mobile:assembleDev_release')
    def copyAndroidDevApksPostBuild = tasks.register('copyAndroidDevApksPostBuild', Copy) {
        dependsOn assembleDevRelease
        from "${projectDir}/dev_release"
        include '**/*.apk'
        into rootProject.releasePath
    }
    assembleDevRelease.finalizedBy(copyAndroidDevApksPostBuild)

    //noinspection ConfigurationAvoidance
    def assembleSecond = tasks.getByPath(':mobile:assembleSecond')
    def copyAndroidSecondApksPostBuild = tasks.register('copyAndroidSecondApksPostBuild', Copy) {
        dependsOn assembleSecond
        from "${projectDir}/second"
        include '**/*.apk'
        into rootProject.releasePath
    }
    assembleSecond.finalizedBy(copyAndroidSecondApksPostBuild)
}